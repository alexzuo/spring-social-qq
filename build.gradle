buildscript {
    repositories {
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:docbook-reference-plugin:0.1.3'
    }
}

configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'com.georges'

    sourceCompatibility=1.5
    targetCompatibility=1.5

    ext {
      springSocialVersion = '1.1.0.BUILD-SNAPSHOT'
      jacksonVersion = '1.9.9'
      jspApiVersion = '2.1'
      junitVersion = '4.10'
      mockitoVersion = '1.9.0'
      servletApiVersion = '2.5'
	  commenLangVersion = '2.6'
      springSecurityCryptoVersion = '3.1.2.RELEASE'
      springVersion = '3.1.2.RELEASE'
      springTestMvcVersion = "1.0.0.BUILD-SNAPSHOT"
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:none']
    [compileJava, compileTestJava]*.options*.encoding = 'utf-8' 
 
    sourceSets.test.resources.srcDirs = ['src/test/resources', 'src/test/java']

    test.systemProperty("java.awt.headless", "true")

    repositories {
        maven { url "http://repo.springsource.org/libs-snapshot" }
    }

    dependencies {
        testCompile "junit:junit-dep:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    // servlet-api (2.5) and tomcat-servlet-api (3.0) classpath entries should not be
    // exported to dependent projects in Eclipse to avoid false compilation errors due
    // to changing APIs across these versions
    eclipse.classpath.file.whenMerged { classpath ->
        classpath.entries.findAll { entry -> entry.path.contains('servlet-api') }*.exported = false
    }
}


project('spring-social-qq') {
    description = 'QQ API'
	
    dependencies {
        compile "org.springframework.social:spring-social-core:$springSocialVersion"
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
		compile "commons-lang:commons-lang:$commenLangVersion"
        testCompile "org.springframework:spring-test-mvc:$springTestMvcVersion"
    }
}

